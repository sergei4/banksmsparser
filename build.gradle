group 'com.skmmobile'
version '1.3'

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// загрузка файла на сервер
apply plugin: 'org.hidetake.ssh'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.hidetake:gradle-ssh-plugin:2.9.0'
    }
}

remotes {
    webServer {
        host = rootProject.ext.uploadConfig.host
        user = rootProject.ext.uploadConfig.user
        password = rootProject.ext.uploadConfig.pwd
    }
}

def desResource  = rootProject.ext.uploadConfig.resFolder

task uploadXmlOnServer {
    group = "extentional"
    doLast {
        ssh.run {
            session(remotes.webServer) {
                put from: file("files/banksmspatterns.xml"), into: desResource
            }
        }
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    repositories {
        maven {
            url version.contains("SNAPSHOT") ? rootProject.ext.maven.snapshot : rootProject.ext.maven.release
            credentials {
                username rootProject.ext.maven.user
                password rootProject.ext.maven.pwd
            }
        }
    }

    publications {
        maven(MavenPublication) {
            artifactId 'banksmsparser'
            from components.java
        }
    }
}